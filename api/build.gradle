plugins {
    id 'org.openapi.generator'
}

apply plugin: 'org.springframework.boot'

ext {
    group = 'com.yotpo.managetask'
    generateServerLocation = "$buildDir/generated/server"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs "$generateServerLocation/src/main/java/"
        }
    }
}

// https://github.com/OpenAPITools/openapi-generator/tree/master/modules/openapi-generator-gradle-plugin
openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/openApi/service.yaml"
    outputDir = "$generateServerLocation"
    apiPackage = "${group}.api.generated"
    invokerPackage = "${group}.api.generated.invoker"
    modelPackage = "${group}.api.generated.model"
//    modelNameSuffix = "Dto"

    // config options "documentation": https://openapi-generator.tech/docs/generators/spring
    configOptions = [
            dateLibrary            : "java8-localdatetime",
            hideGenerationTimestamp: "true",
            interfaceOnly          : "true",
            unhandledException     : "true",
            useTags                : "true"
    ]
}

task generateFullSchemaFile(type: Exec) {
    commandLine "$rootDir/scripts/combine_openapi_files.sh", "$rootDir"
}
compileJava.dependsOn tasks.generateFullSchemaFile

compileJava.dependsOn tasks.openApiGenerate

task cleanAutoGenerateFiles(type: Delete) {
    delete "$generateServerLocation"
}
tasks.openApiGenerate.dependsOn tasks.cleanAutoGenerateFiles

//tasks.withType(Test) {
//    maxParallelForks = Runtime.runtime.availableProcessors()
//}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation "javax.persistence:javax.persistence-api"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.retry:spring-retry:1.2.4.RELEASE'
    // https://mvnrepository.com/artifact/org.json/json
    implementation group: 'org.json', name: 'json', version: '20211205'


    implementation project(":core")
}

test {
    useJUnitPlatform()
}